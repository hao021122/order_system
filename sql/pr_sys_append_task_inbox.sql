IF EXISTS (SELECT * FROM SYS.OBJECTS WHERE NAME = 'pr_sys_append_task_inbox')
BEGIN DROP PROC pr_sys_append_task_inbox END
GO

CREATE PROCEDURE pr_sys_append_task_inbox (
	@current_uid nvarchar(255)
	, @task_inbox_url nvarchar(255)
	, @task nvarchar(max)
	, @task_fk_value nvarchar(255)
	, @module_id int
	, @co_row_guid uniqueidentifier
	, @task_inbox_id uniqueidentifier output
	, @proc_name nvarchar(255)
	, @task_fk_value2 nvarchar(255) = NULL
	, @remarks nvarchar(255) = NULL
	, @task_fk_value3 nvarchar(255) = NULL
)
AS
BEGIN
/*
-- append the record into tb_task_inbox

	SAMPLE:
		DECLARE 
			@id uniqueidentifier

		EXEC pr_sys_append_task_inbox
			@current_uid = 'tester'
			, @task_inbox_url = '~/modukes/admin/test.aspx?id=207869f3-229a-494c-bc01-29eaa3d412d9'
			, @task = 'run test action'
			, @task_fk_value '207869f3-229a-494c-bc01-29eaa3d412d9'
			, @module_id = 210003
			, @co_row_guid = null
			, @task_inbox_id = @id output
			, @proc_name = 'pr_test'
			, @task_fk_value2 nvarchar(255) = NULL
			, @remarks nvarchar(255) = NULL
			, @task_fk_value3 nvarchar(255) = NULL
*/
	SET NOCOUNT ON;

	-- for storing the seqential id generated by server
    DECLARE @tb table (
		id uniqueidentifier
	)

	INSERT INTO tb_task_inbox (
		modified_on, modified_by, created_on, created_by
		, task_inbox_url, task_inbox_status_id, task, task_fk_value, task_fk_value2
		, remarks, module_id
		, co_row_guid
		, task_fk_value3
		, proc_name
	)
	OUTPUT inserted.task_inbox_id INTO @tb
	SELECT
		GETDATE(), @current_uid, GETDATE(), @current_uid
		, @task_inbox_url
		, 0		-- task_inbox_status_id
		, @task
		, @task_fk_value
		, @task_fk_value2
		, @remarks, @module_id
		, @co_row_guid
		, @task_fk_value3
		, @proc_name

	SELECT 
		@task_inbox_id = id
	FROM @tb

	SET NOCOUNT OFF
		
END
GO
